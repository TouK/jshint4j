apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'pl.touk'
version = '2.9.5'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'org.mozilla:rhino:1.7.7.2'

    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'jshint4j'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Java wrapper for JSHint (http://www.jshint.com/).'
                    url 'https://github.com/TouK/jshint4j'

                    scm {
                        connection 'https://github.com/TouK/jshint4j.git'
                        developerConnection 'https://github.com/TouK/jshint4j.git'
                        url 'https://github.com/TouK/jshint4j'
                        url 'https://github.com/gildur/jshint4j'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://raw.githubusercontent.com/TouK/jshint4j/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'gildur'
                            name 'Piotr Wolny'
                            email 'gildur@gmail.com'
                        }
                        developer {
                            id 'tomasz_kalkosinski'
                            name 'Tomasz Kalkosinski'
                        }
                    }
                }
            }
        }
    }
}
