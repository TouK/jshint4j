apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'pl.gildur'
version = '1.0.0'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile 'org.mozilla:rhino:1.7R4'

    testCompile 'junit:junit:4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'jshint4j'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Java wrapper for JSHint (http://www.jshint.com/).'
                url 'https://github.com/gildur/jshint4j'

                scm {
                    connection 'https://github.com/gildur/jshint4j.git'
                    developerConnection 'https://github.com/gildur/jshint4j.git'
                    url 'https://github.com/gildur/jshint4j'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://raw.githubusercontent.com/gildur/jshint4j/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'gildur'
                        name 'Piotr Wolny'
                        email 'gildur@gmail.com'
                    }
                }
            }
        }
    }
}
